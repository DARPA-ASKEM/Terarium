buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group: 'cz.habarta.typescript-generator', name: 'typescript-generator-gradle-plugin', version: '3.1.1185'
	}
}

plugins {
	id "java"
	id "io.quarkus"
}

apply plugin: 'cz.habarta.typescript-generator'

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusVersion}")

	// REST server
	implementation "io.quarkus:quarkus-resteasy-reactive"
	implementation "io.quarkus:quarkus-resteasy-reactive-jackson"
	implementation "io.quarkus:quarkus-smallrye-openapi"
	implementation "io.quarkus:quarkus-arc"
	implementation "io.quarkus:quarkus-logging-json"

	// REST client
	implementation "io.quarkus:quarkus-rest-client-reactive"
	implementation "io.quarkus:quarkus-rest-client-reactive-jackson"

	// Persistence
	implementation "io.quarkus:quarkus-hibernate-orm"
	implementation "io.quarkus:quarkus-jdbc-postgresql"
	implementation "io.quarkus:quarkus-hibernate-orm-panache"
	annotationProcessor "io.quarkus:quarkus-panache-common"

	// Caching
	implementation "io.quarkus:quarkus-cache"
	implementation "io.quarkus:quarkus-scheduler"

	// Message Queue
	implementation "io.quarkus:quarkus-smallrye-reactive-messaging-rabbitmq"

	// Auth dependencies
	implementation "io.quarkus:quarkus-oidc"
	implementation "io.quarkus:quarkus-keycloak-authorization"

	// Misc
	implementation "org.apache.commons:commons-text:1.10.0"
	implementation "org.jsoup:jsoup:1.15.3"
	implementation "com.google.guava:guava:31.1-jre"
	implementation 'commons-io:commons-io:2.11.0'

	// Test
	testImplementation "io.rest-assured:rest-assured"
	testImplementation "io.quarkus:quarkus-test-oidc-server"
	testImplementation "io.quarkus:quarkus-junit5"
	testImplementation "io.quarkus:quarkus-test-keycloak-server"

	// lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"

	// Annotations
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	//elasticsearch
	implementation "org.elasticsearch:elasticsearch:${elasticsearchVersion}"
	implementation "org.elasticsearch.client:elasticsearch-rest-client:${elasticsearchVersion}"
	implementation "org.elasticsearch.client:elasticsearch-rest-high-level-client:${elasticsearchVersion}"
}

group "software.uncharted"
version "2.0.0"

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
	options.encoding = "UTF-8"
	options.compilerArgs << "-parameters"
}

compileTestJava {
	options.encoding = "UTF-8"
}

generateTypeScript {
	jsonLibrary = 'jackson2'
	classesWithAnnotations = [
		'software.uncharted.terarium.hmiserver.annotations.TSModel'
	]
	excludeClasses = [
	    'io.vertx.core.shareddata.impl.ClusterSerializable',
		'java.io.Serializable'
	]
	excludeClassPatterns = [
		'io.quarkus.**'
	]
	excludePropertyAnnotations = [
	    'software.uncharted.terarium.hmiserver.annotations.TSIgnore'
	]
	enumMemberCasing = 'PascalCase'
	mapEnum = 'asEnum'
	nonConstEnums = 'true'
	noFileComment = 'true'
	outputFile = '../../client/hmi-client/src/types/Types.ts'
	outputKind = 'module'
	outputFileType = 'implementationFile'
	optionalAnnotations = [
	    'software.uncharted.terarium.hmiserver.annotations.TSOptional'
	]
}
